plugins {
    id 'com.jfrog.bintray'
    id 'com.google.protobuf' version '0.8.12'
}

description 'Tronj gRPC client classes'

dependencies {
    implementation project(':utils')
    implementation project(':abi')

    implementation 'io.grpc:grpc-netty-shaded:1.31.0'
    implementation 'io.grpc:grpc-netty:1.31.0'
    implementation 'io.grpc:grpc-okhttp:1.31.0'
    implementation 'io.grpc:grpc-protobuf:1.31.0'
    implementation 'io.grpc:grpc-stub:1.31.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

    implementation "org.bouncycastle:bcprov-jdk15on:$bouncycastleVersion"
    implementation 'org.apache.tuweni:tuweni-bytes:1.0.0'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.12.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.31.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc { }
        }
    }
    generatedFilesBaseDir = "$projectDir/src"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives']

    pkg {
        repo = 'tronj'
        name = 'client'
        // configurations = ['archives']
        desc = description
        websiteUrl = 'https://github.com/tronprotocol/tronj'
        vcsUrl = 'https://github.com/tronprotocol/tronj.git'
        licenses = ['Apache-2.0']
        publish = true
        publicDownloadNumbers = true
        version {
            afterEvaluate {
                name = "${project.version}"
                released = new Date()
            }
        }
    }
}
